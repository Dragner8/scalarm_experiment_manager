
class window.InputSpace

    constructor: (@experimentInput) ->
        InputSpace.experimentInput = @experimentInput
        document.addEventListener("parametrizationTypeChange", @parametrizationTypeChangeListener, false)

        @bindParametrizationListener()

        nextId = 1
        while $("select#parametrization_type_#{nextId}").length > 0
            @parameterValuesPartial(nextId)
            nextId += 1


    @getParameter: (entityGroupId, entityId, parameterId) ->
        entityGroup = (group for group in InputSpace.experimentInput when group.id == entityGroupId)[0]
        entity = (entity for entity in entityGroup.entities when entity.id == entityId)[0]
        parameter = (parameter for parameter in entity.parameters when parameter.id == parameterId)[0]

        entityGroup: entityGroup
        entity: entity
        parameter: parameter


    updateAllInputParameterValues: () =>
        $("div[id^='parameter_values_'] input").each (_, element) =>
            groupId = $(element).parents("div[group_id]").attr("group_id")
            entityId = $(element).parents("div[entity_id]").attr("entity_id")
            parameterId = $(element).parents("div[parameter_id]").attr("parameter_id")

            parameter = InputSpace.getParameter(groupId, entityId, parameterId)

            parameterElementId = $(element).attr("id")
            newValue = $(element).val()

            parameterIndex = parameterElementId.substring(parameterElementId.lastIndexOf("_") + 1)
            parameter.parameter["parametrizationType"] = $("#parametrization_type_" + parameterIndex).val()
            @updateParameterValuesInJSON(parameter.parameter, parameterElementId, newValue)

        $("input[name='experiment_input']").val(JSON.stringify(@experimentInput))


    bindParametrizationListener: () ->
        $("[data-slug='parametrization_types'] dd[entity-group-id]").each (groupIndex, entityGroup) =>
            entityGroupId = $(entityGroup).attr('entity-group-id')

            $(entityGroup).find("[entity-id]").each (entityIndex, entity) =>
                entityId = $(entity).attr('entity-id')

                $(entity).find("[parameter]").each (_, parameter) =>
                    parameterInfo = JSON.parse($(parameter).attr('parameter'))
                    parameterId = parameterInfo.id
                    [_, ..., parameterIndex] = $(parameter).attr('id').split('_')

                    $(parameter).on 'change', =>
                        selectedType = $(parameter).val()
                        # dispatch event to any subscribed listener
                        changeEvent = new CustomEvent "parametrizationTypeChange",
                            detail:
                                parametrizationType: selectedType,
                                entityGroupId: entityGroupId,
                                entityId: entityId,
                                parameterId: parameterId,
                                parameterIndex: parameterIndex
                            bubbles: true,
                            cancelable: true

                        document.dispatchEvent(changeEvent)


    parametrizationTypeChangeListener: (event) =>
        info = event.detail
        # change DOM in the parameter values tab
        @parameterValuesPartial(info.parameterIndex)
        # update experiment input json document
        @updateParametrizationTypeInJSON(info, info.parametrizationType)


# updates experiment input model stored in global 'experimentInput' variable as JSON
    updateParametrizationTypeInJSON: (parameterInfo, newParametrizationType) =>
        parameter = InputSpace.getParameter(parameterInfo.entityGroupId, parameterInfo.entityId,
                parameterInfo.parameterId)
        parameter.parameter["parametrizationType"] = newParametrizationType

# generic function for building DOM structure for input parameter
    parameterValuesPartial: (parameterIndex) =>
        selectElement = $("select#parametrization_type_" + parameterIndex)
        parameter = $.parseJSON(selectElement.attr("parameter"))

        if parameter.type == "integer"
            parameterValuesPartialForInteger(parameter, selectElement.val(), parameterIndex)

        else if parameter.type == "float"
            parameterValuesPartialForFloat(parameter, selectElement.val(), parameterIndex)

        else if parameter.type == "string"
            parameterValuesPartialForString(parameter, selectElement.val(), parameterIndex)

        $("#parameter_values_#{parameterIndex} input").on "change paste keyup", (event) =>
            $input = $(event.currentTarget)
            entityGroupId = $input.parents("div[group_id]").attr("group_id")
            entityId = $input.parents("div[entity_id]").attr("entity_id")
            parameterId = $input.parents("div[parameter_id]").attr("parameter_id")

            parameter = InputSpace.getParameter(entityGroupId, entityId, parameterId)

            @updateParameterValuesInJSON(parameter.parameter, $input.attr("id"), $input.val())


# updates experiment input model stored in global 'experimentInput' variable as JSON
# currently supporting types: value, gauss, uniform, range
    updateParameterValuesInJSON: (parameter, parameterElementId, newValue) =>
        if parameter.parametrizationType == undefined or parameter.parametrizationType == "value"
            parameter["value"] = $("##{parameterElementId}").val()

        else if parameter.parametrizationType == "gauss"
            if parameterElementId.indexOf("mean") >= 0
                parameter["mean"] = newValue

            else if parameterElementId.indexOf("variance") >= 0
                parameter["variance"] = newValue

        else if parameter.parametrizationType == "uniform"
            if parameterElementId.indexOf("min") >= 0
                parameter["min"] = newValue

            else if parameterElementId.indexOf("max") >= 0
                parameter["max"] = newValue

        else if parameter.parametrizationType == "range"
            if parameterElementId.indexOf("min") >= 0
                parameter["min"] = newValue

            else if parameterElementId.indexOf("max") >= 0
                parameter["max"] = newValue

            else if parameterElementId.indexOf("step") >= 0
                parameter["step"] = newValue

class window.ExperimentDesigner

    constructor: (@inputSpace) ->
        @doeGroups = []
        @rangeParameters = []
        @nextId = 0

        document.addEventListener("parametrizationTypeChange", @parametrizationTypeChangeListener, false)
        $("body").delegate("#experiment_submit_form", "submit", @updateDoeForSubmit)
        $("body").delegate("button#check-experiment-size", "click", @checkExperimentSize)


    parametrizationTypeChangeListener: (event) =>
        parameterId = [event.detail.entityGroupId, event.detail.entityId, event.detail.parameterId].join("___")

        if event.detail.parametrizationType == "range"
            @addRangeParameter(parameterId)

        else
            # remove already parameter if already chosen
            $("[id^='doe-group-'] li.bullet-item").each (index, bulletItem) =>
                if $(bulletItem).is("[param_id]") and ($(bulletItem).attr('param_id') == parameterId)
                    $(bulletItem).find("a.button").click()
                # remove the parameter from the parameter list
                @removeRangeParameter(parameterId)

        @updateSelectElements()

    # updating all select elements with parameters
    updateSelectElements: () =>
        $("[id^='doe-group-']").each (_, doeGroup) =>
            @addParameterSelectorToGroup($(doeGroup).attr('id'))

    addParameterSelectorToGroup: (doeGroupId) =>
        selectElement = $("##{doeGroupId} select#parameters").html('')

        for p in @rangeParameters
            parameterLabel = "#{p.entityGroup['label']} - #{p.entity['label']} - #{p.parameter['label']}"
            option = $("<option value='#{p.fullId}'>#{parameterLabel}</option>")
            selectElement.append(option)

        # disable button if there is no options
        if selectElement.html() == ''
            $('#' + doeGroupId + " select#parameters").hide()
            $('#' + doeGroupId + " .price a.button").hide()
        else # enable otherwise
            $('#' + doeGroupId + " select#parameters").show()
            $('#' + doeGroupId + " .price a.button").show()

    createDoeGroup: (selectId) =>
        select = $('#' + selectId)

        doe_label = select.find("option[value='" + select.val() + "']").html()
        doe_id = select.val()

        group = $('#doe-group-template').clone()

        group.find('li.title').html(doe_label)
        group.attr('id', 'doe-group-' + @nextId)
        group.attr('doe-id', doe_id)

        $("div.content[data-slug='doe']").append(group)
        @addParameterSelectorToGroup('doe-group-' + @nextId)
        group.show()

        @nextId += 1


    addParameterToGroup: (groupElement) =>
        select = groupElement.find('select#parameters')
        param_label = select.find("option[value='" + select.val() + "']").html()
        param_id = select.val()

        # button removing this parameter from its group
        deleteButton = $("<a href='#'></a>").addClass("button radius").on 'click', (event) =>
            $button = $(event.currentTarget)
            # get parameter id to remove
            parameterId = $button.closest('li').attr('param_id')
            @addRangeParameter(parameterId)
            # update other select element
            @updateSelectElements()
            # remove the row describing parameter
            $button.closest('li').remove()
            # prevent from reloading
            false

        deleteButton.html("<%= I18n.t('simulations.conduct.doe.remove_parameter') %>")
        # parameter label
        content = $("<label></label>").css('margin-bottom', '5px').html(param_label)
        # row describing the parameter
        paramElement = $("<li></li>").addClass('bullet-item').attr('param_id',
                param_id).append(content).append(deleteButton)

        # append this parameter row
        groupElement.find('li.price').after(paramElement)
        # remove parameter id from range parameter list
        @removeRangeParameter(param_id)
        #update select elements
        @updateSelectElements()


    deleteGroup: (groupElement) =>
        groupElement.find(".bullet-item a.button").click()
        groupElement.remove()


    updateDoeForSubmit: () =>
        doeTab = []

        $("[id^='doe-group-']").each (index, doeGroup) =>
            doeId = $(doeGroup).attr('doe-id')

            if(doeId != undefined)
                parameters = []

                $(doeGroup).find('li.bullet-item').each (i, parameterBullet) =>
                    parameters.push($(parameterBullet).attr('param_id'))

                doeTab.push([ doeId, parameters ])

        $("input[name='doe']").val(JSON.stringify(doeTab))
        $("#doe").val(JSON.stringify(doeTab))


    checkExperimentSize: () =>
        @inputSpace.updateAllInputParameterValues()
        @updateDoeForSubmit()

        $.ajax
            type: "POST",
            url: $('#calculate-experiment-size-url').val(),
            data:
                simulation_id: $('#simulation_id').val(),
                experiment_input: $('#experiment_input').val(),
                doe: $('#doe').val(),
                replication_level: $('#replication_level').val()
            beforeSend: (xhr) =>
                $('#conduct-loading').show()
            success: (msg) =>
                $('#conduct-loading').hide()
                $("#experiment-size-dialog #calculated-experiment-size").html(msg.experiment_size)

                if(msg.error != undefined and msg.error != null)
                    toastr.error(msg.error)
                else
                    $('#experiment-size-dialog').foundation('reveal', 'open')
            error: (msg) ->
                toastr.error(msg)
                $('#conduct-loading').hide()

        return false


    # add a parameter to the internal range parameter list
    addRangeParameter: (fullParameterId) =>
        id_info = fullParameterId.split('___')
        parameter = InputSpace.getParameter(id_info[0], id_info[1], id_info[2])

        @rangeParameters.push(
            fullId: fullParameterId
            entityGroup: parameter.entityGroup
            entity: parameter.entity
            parameter: parameter.parameter
        )

    # remove the parameter from the internal range parameter list
    removeRangeParameter: (fullParameterId) =>
#        console.log "Removing: #{fullParameterId}"
        index = -1
        for parameter, i in @rangeParameters
#            console.log "#{parameter.fullId} --- #{parameter.fullId == fullParameterId}"
            if parameter.fullId == fullParameterId
                index = i
        @rangeParameters.splice(index, 1) if index >= 0