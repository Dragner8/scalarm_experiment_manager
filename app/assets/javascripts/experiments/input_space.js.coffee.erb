class window.InputSpace

    constructor: (@experimentInput) ->
        InputSpace.experimentInput = @experimentInput
        document.addEventListener("parametrizationTypeChange", @parametrizationTypeChangeListener, false)

        @bindParametrizationListener()

        nextId = 1
        while $("select#parametrization_type_#{nextId}").length > 0
            @parameterValuesPartial(nextId)
            nextId += 1


    @getParameter: (entityGroupId, entityId, parameterId) ->
        if (parameterId == undefined || parameterId == "") && entityId != undefined && entityId != ""
            parameterId = entityId
            entityId = undefined

        if (parameterId == undefined || parameterId == "") && entityGroupId != undefined && entityGroupId != ""
            parameterId = entityGroupId
            entityGroupId = undefined

        entityGroup = (group for group in InputSpace.experimentInput when (group.id == entityGroupId) ||
            ( (group.id == undefined) && (entityGroupId == "") ) )[0]

        entity = (entity for entity in entityGroup.entities when (entity.id == entityId) ||
            ( (entity.id == undefined) && (entityId == "") ) )[0]

        parameter = (parameter for parameter in entity.parameters when (parameter.id == parameterId) ||
            ( (parameter.id == undefined) && (parameterId == "") ) )[0]

        entityGroup: entityGroup
        entity: entity
        parameter: parameter


    updateAllInputParameterValues: () =>
        $("fieldset[class^='parameter_']").each (_, element) =>
            parametrization = $(element).find("select[id^='parametrization_type_']").val()
            $parameterElement = $(element).find("div[id^='parameter_values_']")

            groupId = $parameterElement.attr("group_id")
            entityId = $parameterElement.attr("entity_id")
            parameterId = $parameterElement.attr("parameter_id")

            parameter = InputSpace.getParameter(groupId, entityId, parameterId)

            $(element).find("[parametrization-type='#{parametrization}']").find("input, textarea").each (_, inputElement) =>
                parameterElementId = $(inputElement).attr("id")
                newValue = $(inputElement).val()

                parameter.parameter["parametrizationType"] = parametrization
                @updateParameterValuesInJSON(parameter.parameter, parameterElementId, newValue)

        $("input[name='experiment_input']").val(JSON.stringify(@experimentInput))


    bindParametrizationListener: () ->
        $("[data-slug='parametrization_types'] dd[entity-group-id]").each (groupIndex, entityGroup) =>
            entityGroupId = $(entityGroup).attr('entity-group-id')

            $(entityGroup).find("[entity-id]").each (entityIndex, entity) =>
                entityId = $(entity).attr('entity-id')

                $(entity).find("[parameter]").each (_, parameter) =>
                    parameterInfo = JSON.parse($(parameter).attr('parameter'))
                    parameterId = parameterInfo.id
                    [_, ..., parameterIndex] = $(parameter).attr('id').split('_')

                    $(parameter).on 'change', =>
                        selectedType = $(parameter).val()
                        # dispatch event to any subscribed listener
                        changeEvent = new CustomEvent "parametrizationTypeChange",
                            detail:
                                parametrizationType: selectedType,
                                entityGroupId: entityGroupId,
                                entityId: entityId,
                                parameterId: parameterId,
                                parameterIndex: parameterIndex
                            bubbles: true,
                            cancelable: true

                        document.dispatchEvent(changeEvent)


    parametrizationTypeChangeListener: (event) =>
        info = event.detail
        # change DOM in the parameter values tab
        @parameterValuesPartial(info.parameterIndex)
        # update experiment input json document
        @updateParametrizationTypeInJSON(info, info.parametrizationType)


# updates experiment input model stored in global 'experimentInput' variable as JSON
    updateParametrizationTypeInJSON: (parameterInfo, newParametrizationType) =>
        parameter = InputSpace.getParameter(parameterInfo.entityGroupId, parameterInfo.entityId,
                parameterInfo.parameterId)
        parameter.parameter["parametrizationType"] = newParametrizationType

# generic function for building DOM structure for input parameter
    parameterValuesPartial: (parameterIndex) =>
        selectElement = $("select#parametrization_type_" + parameterIndex)
        parameter = $.parseJSON(selectElement.attr("parameter"))
        parametrizationType = selectElement.val()

        $(".parameter_#{parameterIndex} [parametrization-type]").hide()
        $(".parameter_#{parameterIndex} [parametrization-type='#{parametrizationType}']").show()

        $("#parameter_values_#{parameterIndex} input:visible, #parameter_values_#{parameterIndex} textarea:visible").on "change paste keyup", (event) =>
            $input = $(event.currentTarget)
            entityGroupId = $input.parents("div[group_id]").attr("group_id")
            entityId = $input.parents("div[entity_id]").attr("entity_id")
            parameterId = $input.parents("div[parameter_id]").attr("parameter_id")

            parameter = InputSpace.getParameter(entityGroupId, entityId, parameterId)

            @updateParameterValuesInJSON(parameter.parameter, $input.attr("id"), $input.val())


# updates experiment input model stored in global 'experimentInput' variable as JSON
# currently supporting types: value, gauss, uniform, range
    updateParameterValuesInJSON: (parameter, parameterElementId, newValue) =>

        if parameter.parametrizationType == undefined or parameter.parametrizationType == "value"
            parameter["value"] = $("##{parameterElementId}").val()

        else if parameter.parametrizationType == "gauss"
            if parameterElementId.indexOf("mean") >= 0
                parameter["mean"] = newValue

            else if parameterElementId.indexOf("variance") >= 0
                parameter["variance"] = newValue

        else if parameter.parametrizationType == "uniform"
            if parameterElementId.indexOf("min") >= 0
                parameter["min"] = newValue

            else if parameterElementId.indexOf("max") >= 0
                parameter["max"] = newValue

        else if parameter.parametrizationType == "range"
            if parameterElementId.indexOf("min") >= 0
                parameter["min"] = newValue

            else if parameterElementId.indexOf("max") >= 0
                parameter["max"] = newValue

            else if parameterElementId.indexOf("step") >= 0
                parameter["step"] = newValue

        else if parameter.parametrizationType == "custom"
            parameter["custom_values"] = newValue
